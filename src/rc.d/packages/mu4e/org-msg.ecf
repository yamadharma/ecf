;;; -*- mode: emacs-lisp; lexical-binding: t; coding: utf-8-unix; -*-
;;; OrgMsg is a GNU/Emacs global minor mode mixing up Org mode and Message mode to compose and reply to emails in a Outlook HTML friendly style
;;; https://github.com/jeremy-compostella/org-msg

(require 'org-msg)

(setq org-msg-options "html-postamble:nil H:5 num:nil ^:{} toc:nil author:nil email:nil \\n:t tex:dvipng"
      org-msg-startup "hidestars indent inlineimages"
      org-msg-greeting-name-limit 3
      org-msg-default-alternatives '((new . (utf-8 html))
                                     (reply-to-text . (utf-8 html))
                                     (reply-to-html . (utf-8 html)))
      org-msg-convert-citation t
      ;; The default attachment matcher gives too many false positives,
      ;; it's better to be more conservative. See https://regex101.com/r/EtaiSP/4.
      org-msg-attached-file-reference
      "see[ \t\n]\\(?:the[ \t\n]\\)?\\(?:\\w+[ \t\n]\\)\\{0,3\\}\\(?:attached\\|enclosed\\)\\|\
(\\(?:attached\\|enclosed\\))\\|\
\\(?:attached\\|enclosed\\)[ \t\n]\\(?:for\\|is\\)[ \t\n]")

(setq org-msg-greeting-fmt "\nHi%s,\n\n")

(setq mu4e-compose-signature-auto-include nil)
(setq org-msg-signature "
Kind regards,
#+begin_signature
--
Dmitry S. Kulyabov
#+end_signature")

(org-msg-mode)

;;; Fix for mu-1.12.0

(defun org-msg-edit-mode-mu4e ()
  "Setup mu4e faces, addresses completion and run mu4e."
  (mu4e--compose-remap-faces)
  (unless (mu4e-running-p)
    (if (fboundp #'mu4e~start) (mu4e~start) (mu4e--start)))
  (when mu4e-compose-complete-addresses
    (mu4e--compose-setup-completion))

  ;; the following code is verbatim from mu4e-compose.el, `mu4e-compose-mode'

  ;; set the attachment dir to something more reasonable than the draft
  ;; directory.
  (setq default-directory (mu4e-determine-attachment-dir))

  (add-hook 'before-save-hook  #'mu4e--compose-before-save nil t)
  (add-hook 'after-save-hook   #'mu4e--compose-after-save nil t)
  (add-hook 'message-send-hook #'mu4e--compose-before-send nil t)
  (add-hook 'message-sent-hook #'mu4e--compose-after-send nil t)
  
  (mu4e--compose-set-friendly-buffer-name)
  (let ((message-hidden-headers mu4e-compose-hidden-headers))
    (message-hide-headers))

  ;; jump to some reasonable place.
  (if (not (message-field-value "To"))
      (message-goto-to)
    (if (not (message-field-value "Subject"))
        (message-goto-subject)
      (message-goto-body)))
  ;; buffer is not user-modified yet
  (set-buffer-modified-p nil)
  (undo-boundary))
  
;;;
