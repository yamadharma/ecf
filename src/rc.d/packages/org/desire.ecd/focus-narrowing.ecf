;; Narrowing to a subtree with bh/org-todo

;; f5 and S-f5 are bound the functions for narrowing and widening the emacs buffer as defined below.
;; We now use:
;;    T (tasks) for C-c / t on the current buffer
;;    N (narrow) narrows to this task subtree
;;    U (up) narrows to the immediate parent task subtree without moving
;;    P (project) narrows to the parent project subtree without moving
;;    F (file) narrows to the current file or file of the existing restriction
;; The agenda keeps widening the org buffer so this gives a convenient way to focus on what we are doing. 

(global-set-key (kbd "<f5>") 'ecf/org-todo)

(defun ecf/org-todo (arg)
  (interactive "p")
  (if (equal arg 4)
      (save-restriction
        (ecf/narrow-to-org-subtree)
        (org-show-todo-tree nil))
    (ecf/narrow-to-org-subtree)
    (org-show-todo-tree nil)))

(global-set-key (kbd "<S-f5>") 'ecf/widen)

(defun ecf/widen ()
  (interactive)
  (if (equal major-mode 'org-agenda-mode)
      (progn
        (org-agenda-remove-restriction-lock)
        (when org-agenda-sticky
          (org-agenda-redo)))
    (widen)))

(add-hook 'org-agenda-mode-hook
          #'(lambda () (org-defkey org-agenda-mode-map "W" (lambda () (interactive) (setq ecf/hide-scheduled-and-waiting-next-tasks t) (ecf/widen))))
          'append)

(defun ecf/restrict-to-file-or-follow (arg)
  "Set agenda restriction to 'file or with argument invoke follow mode.
I don't use follow mode very often but I restrict to file all the time
so change the default 'F' binding in the agenda to allow both"
  (interactive "p")
  (if (equal arg 4)
      (org-agenda-follow-mode)
    (widen)
    (ecf/set-agenda-restriction-lock 4)
    (org-agenda-redo)
    (beginning-of-buffer)))

(add-hook 'org-agenda-mode-hook
          #'(lambda () (org-defkey org-agenda-mode-map "F" 'ecf/restrict-to-file-or-follow))
          'append)

(defun ecf/narrow-to-org-subtree ()
  (widen)
  (org-narrow-to-subtree)
  (save-restriction
    (org-agenda-set-restriction-lock)))

(defun ecf/narrow-to-subtree ()
  (interactive)
  (if (equal major-mode 'org-agenda-mode)
      (progn
        (org-with-point-at (org-get-at-bol 'org-hd-marker)
          (ecf/narrow-to-org-subtree))
        (when org-agenda-sticky
          (org-agenda-redo)))
    (ecf/narrow-to-org-subtree)))

(add-hook 'org-agenda-mode-hook
          #'(lambda () (org-defkey org-agenda-mode-map "N" 'ecf/narrow-to-subtree))
          'append)

(defun ecf/narrow-up-one-org-level ()
  (widen)
  (save-excursion
    (outline-up-heading 1 'invisible-ok)
    (ecf/narrow-to-org-subtree)))

(defun ecf/get-pom-from-agenda-restriction-or-point ()
  (or (and (marker-position org-agenda-restrict-begin) org-agenda-restrict-begin)
      (org-get-at-bol 'org-hd-marker)
      (and (equal major-mode 'org-mode) (point))
      org-clock-marker))

(defun ecf/narrow-up-one-level ()
  (interactive)
  (if (equal major-mode 'org-agenda-mode)
      (progn
        (org-with-point-at (ecf/get-pom-from-agenda-restriction-or-point)
          (ecf/narrow-up-one-org-level))
        (org-agenda-redo))
    (ecf/narrow-up-one-org-level)))

(add-hook 'org-agenda-mode-hook
          #'(lambda () (org-defkey org-agenda-mode-map "U" 'ecf/narrow-up-one-level))
          'append)

(defun ecf/narrow-to-org-project ()
  (widen)
  (save-excursion
    (ecf/find-project-task)
    (ecf/narrow-to-org-subtree)))

(defun ecf/narrow-to-project ()
  (interactive)
  (if (equal major-mode 'org-agenda-mode)
      (progn
        (org-with-point-at (ecf/get-pom-from-agenda-restriction-or-point)
          (ecf/narrow-to-org-project)
          (save-excursion
            (ecf/find-project-task)
            (org-agenda-set-restriction-lock)))
        (org-agenda-redo)
        (beginning-of-buffer))
    (ecf/narrow-to-org-project)
    (save-restriction
      (org-agenda-set-restriction-lock))))

(add-hook 'org-agenda-mode-hook
          #'(lambda () (org-defkey org-agenda-mode-map "P" 'ecf/narrow-to-project))
          'append)

(defvar ecf/project-list nil)

(defun ecf/view-next-project ()
  (interactive)
  (let (num-project-left current-project)
    (unless (marker-position org-agenda-restrict-begin)
      (goto-char (point-min))
      ; Clear all of the existing markers on the list
      (while ecf/project-list
        (set-marker (pop ecf/project-list) nil))
      (re-search-forward "Tasks to Refile")
      (forward-visible-line 1))

    ; Build a new project marker list
    (unless ecf/project-list
      (while (< (point) (point-max))
        (while (and (< (point) (point-max))
                    (or (not (org-get-at-bol 'org-hd-marker))
                        (org-with-point-at (org-get-at-bol 'org-hd-marker)
                          (or (not (ecf/is-project-p))
                              (ecf/is-project-subtree-p)))))
          (forward-visible-line 1))
        (when (< (point) (point-max))
          (add-to-list 'ecf/project-list (copy-marker (org-get-at-bol 'org-hd-marker)) 'append))
        (forward-visible-line 1)))

    ; Pop off the first marker on the list and display
    (setq current-project (pop ecf/project-list))
    (when current-project
      (org-with-point-at current-project
        (setq ecf/hide-scheduled-and-waiting-next-tasks nil)
        (ecf/narrow-to-project))
      ; Remove the marker
      (setq current-project nil)
      (org-agenda-redo)
      (beginning-of-buffer)
      (setq num-projects-left (length ecf/project-list))
      (if (> num-projects-left 0)
          (message "%s projects left to view" num-projects-left)
        (beginning-of-buffer)
        (setq ecf/hide-scheduled-and-waiting-next-tasks t)
        (error "All projects viewed.")))))

(add-hook 'org-agenda-mode-hook
          #'(lambda () (org-defkey org-agenda-mode-map "V" 'ecf/view-next-project))
          'append)

;; Force showing the next headline

(setq org-show-entry-below (quote ((default))))

;;;
